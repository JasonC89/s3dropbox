apply plugin: 'java'
apply plugin: 'idea'

defaultTasks 'clean', 'build'

version = '1.8.1'

project.ext.mainClassName = 'com.tomczarniecki.s3.Main'

repositories {
  mavenCentral()
  flatDir { dirs 'repo' }
}

configurations.all {
  exclude(group: 'commons-logging')
  exclude(module: 'hamcrest-core')
}

dependencies {
  compile 'joda-time:joda-time:2.1'
  compile 'org.slf4j:slf4j-simple:1.6.6'
  compile 'org.slf4j:jcl-over-slf4j:1.6.6'

  compile 'commons-io:commons-io:2.4'
  compile 'commons-cli:commons-cli:1.2'
  compile 'commons-lang:commons-lang:2.6'

  compile 'com.jgoodies:forms:1.2.1'
  compile 'com.jgoodies:looks:2.2.2'

  compile 'com.amazonaws:aws-java-sdk:1.3.22'

  testCompile 'junit:junit:4.10'
  testCompile 'org.mockito:mockito-core:1.9.0'
  testCompile 'org.hamcrest:hamcrest-all:1.1'
  testCompile 'com.googlecode.windowlicker:windowlicker-swing:r268'
}

task(run, type: JavaExec, dependsOn: 'classes') {
  classpath = sourceSets.main.runtimeClasspath
  main = project.ext.mainClassName
}

jar {
  archiveName = 'main.jar'
  manifest {
    attributes(
        'Main-Class': project.ext.mainClassName,
        'Implementation-Title': project.name,
        'Implementation-Version': version
    )
  }
}

task(onejar, dependsOn: 'jar') << {
  def projectName = project.name
  def archivePath = jar.archivePath
  def archiveVersion = version
  def basedir = projectDir
  ant {
    taskdef(name: 'onejar', classname: 'com.simontuffs.onejar.ant.OneJarTask') {
      classpath { fileset(dir: 'etc/one-jar') }
    }
    onejar(destfile: "${libsDir}/${rootProject.name}-${version}.jar") {
      manifest {
        attribute(name: 'Implementation-Title', value: projectName)
        attribute(name: 'Implementation-Version', value: archiveVersion)
      }
      main(jar: archivePath)
      lib {
        configurations.compile.addToAntBuilder(ant, 'fileset', FileCollection.AntType.FileSet)
      }
      fileset(dir: basedir) {
        include(name: 'LICENSE.txt')
      }
    }
  }
}

/*
task(wrapper, type: Wrapper) {
  gradleVersion = '1.2'
}
*/
